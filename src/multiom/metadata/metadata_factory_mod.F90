
! Include preprocessor utils
#include "output_manager_preprocessor_utils.h"
#include "output_manager_preprocessor_trace_utils.h"
#include "output_manager_preprocessor_logging_utils.h"
#include "output_manager_preprocessor_errhdl_utils.h"


#define PP_FILE_NAME 'cached_encoder_mod.F90'
#define PP_SECTION_TYPE 'MODULE'
#define PP_SECTION_NAME 'METADATA_FACTORY_MOD'
MODULE METADATA_FACTORY_MOD

IMPLICIT NONE

!> Default vibibility
PRIVATE

INTERFACE MAKE_METADATA
  MODULE PROCEDURE MAKE_MULTIO_METADATA_FROM_SAMPLE_NAME
  MODULE PROCEDURE MAKE_METADATA_FROM_SAMPLE_NAME
  MODULE PROCEDURE CLONE_METADATA
  MODULE PROCEDURE CLONE_METADATA_AND_LOAD_SAMPLE_NAME
END INTERFACE MAKE_METADATA

!> Public symbols
PUBLIC :: MAKE_METADATA
PUBLIC :: DESTROY_METADATA

CONTAINS

#define PP_PROCEDURE_TYPE 'FUNCTION'
#define PP_PROCEDURE_NAME 'MAKE_MULTIO_METADATA_FROM_SAMPLE_NAME'
PP_THREAD_SAFE FUNCTION  MAKE_MULTIO_METADATA_FROM_SAMPLE_NAME( OUT_METADATA, KIND, SAMPLE_NAME, MIOH, HOOKS ) RESULT(RET)

  ! Symbols imported from other modules within the project.
  USE :: DATAKINDS_DEF_MOD,   ONLY: JPIB_K
  USE :: HOOKS_MOD,           ONLY: HOOKS_T
  USE :: METADATA_BASE_MOD,   ONLY: METADATA_BASE_A
  USE :: MULTIO_METADATA_MOD, ONLY: MULTIO_METADATA_T
  USE :: MULTIO_API,          ONLY: MULTIO_HANDLE

  ! Symbols imported by the preprocessor for debugging purposes
  PP_DEBUG_USE_VARS

  ! Symbols imported by the preprocessor for logging purposes
  PP_LOG_USE_VARS

  ! Symbols imported by the preprocessor for tracing purposes
  PP_TRACE_USE_VARS

IMPLICIT NONE

  !> Dummy arguments
  CLASS(METADATA_BASE_A), POINTER, INTENT(OUT)   :: OUT_METADATA
  CHARACTER(LEN=*),                INTENT(IN)    :: KIND
  CHARACTER(LEN=*),                INTENT(IN)    :: SAMPLE_NAME
  TYPE(MULTIO_HANDLE), TARGET,     INTENT(IN)    :: MIOH
  TYPE(HOOKS_T),                   INTENT(INOUT) :: HOOKS

  !> Function result
  INTEGER(KIND=JPIB_K) :: RET

  !> Local variables
  INTEGER(KIND=JPIB_K) :: ALLOC_STATUS
  CHARACTER(LEN=:), ALLOCATABLE :: ERRMSG
  LOGICAL :: INITIALIZED

  !> Local error flag
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNKNOWN_TYPE=1_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_ALLOCATE=2_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_INITIALIZE=3_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED=5_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED=6_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED=7_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_SET_MIOH=8_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_CHECK_INITIALIZATION=9_JPIB_K

  ! KIND to be used to allocate logging metadata insted of multio

  ! Local variables declared by the preprocessor for debugging purposes
  PP_DEBUG_DECL_VARS

  ! Local variables declared by the preprocessor for logging purposes
  PP_LOG_DECL_VARS

  ! Local variables declared by the preprocessor for tracing purposes
  PP_TRACE_DECL_VARS

  ! Trace begin of procedure
  PP_TRACE_ENTER_PROCEDURE()

  ! Initialization of good path return value
  PP_SET_ERR_SUCCESS( RET )

  ! Error handling
  PP_DEBUG_CRITICAL_COND_THROW( ASSOCIATED(OUT_METADATA), ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED )

  ! Initialize the output metadata
  NULLIFY( OUT_METADATA )

  ! According to the type of metadata create the right object
  ALLOCATE( MULTIO_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
  PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )

  ! Se the multio handle
  SELECT TYPE(MD => OUT_METADATA)
  CLASS IS( MULTIO_METADATA_T )
    PP_TRYCALL(ERRFLAG_UNABLE_TO_SET_MIOH) MD%SET_MULTIO_HANDLE( MIOH, HOOKS )
  CLASS DEFAULT
    PP_DEBUG_CRITICAL_THROW( ERRFLAG_UNKNOWN_TYPE )
  END SELECT

  ! Initialize frome sample name
  PP_TRYCALL(ERRFLAG_UNABLE_TO_INITIALIZE) OUT_METADATA%INIT_FROM_SAMPLE_NAME( SAMPLE_NAME, HOOKS )

  ! Erro handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.ASSOCIATED(OUT_METADATA), ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED )

  ! Check initialization
  PP_TRYCALL(ERRFLAG_UNABLE_CHECK_INITIALIZATION) OUT_METADATA%INITIALIZED( INITIALIZED, HOOKS )
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.INITIALIZED, ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED )

  ! Trace end of procedure (on success)
  PP_TRACE_EXIT_PROCEDURE_ON_SUCCESS()

  ! Exit point (On success)
  RETURN

! Error handler
PP_ERROR_HANDLER

  ! Initialization of bad path return value
  PP_SET_ERR_FAILURE( RET )

#if defined( PP_DEBUG_ENABLE_ERROR_HANDLING )
!$omp critical(ERROR_HANDLER)

  BLOCK

    ! Error handling variables
    PP_DEBUG_PUSH_FRAME()

    ! Handle different errors
    SELECT CASE(ERRIDX)
    CASE (ERRFLAG_UNKNOWN_TYPE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unknown metadata type' )
    CASE (ERRFLAG_UNABLE_TO_ALLOCATE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to allocate memory for metadata' )
      IF ( ALLOCATED(ERRMSG) ) THEN
        PP_DEBUG_PUSH_MSG_TO_FRAME( 'error message: ' // TRIM(ERRMSG) )
      ENDIF
    CASE (ERRFLAG_UNABLE_TO_INITIALIZE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to initialize metadata' )
    CASE (ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata already associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not initialized' )
    CASE (ERRFLAG_UNABLE_TO_SET_MIOH)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to set multio handle' )
    CASE (ERRFLAG_UNABLE_CHECK_INITIALIZATION)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to check initialization' )
    CASE DEFAULT
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unhandled error' )
    END SELECT

    ! Trace end of procedure (on error)
    PP_TRACE_EXIT_PROCEDURE_ON_ERROR()

    ! Write the error message and stop the program
    PP_DEBUG_ABORT

  END BLOCK

!$omp end critical(ERROR_HANDLER)
#endif

  ! Exit point (on error)
  RETURN


END FUNCTION MAKE_MULTIO_METADATA_FROM_SAMPLE_NAME
#undef PP_PROCEDURE_NAME
#undef PP_PROCEDURE_TYPE

#define PP_PROCEDURE_TYPE 'FUNCTION'
#define PP_PROCEDURE_NAME 'MAKE_METADATA_FROM_SAMPLE_NAME'
PP_THREAD_SAFE FUNCTION  MAKE_METADATA_FROM_SAMPLE_NAME( OUT_METADATA, KIND, SAMPLE_NAME, HOOKS ) RESULT(RET)

  ! Symbols imported from other modules within the project.
  USE :: DATAKINDS_DEF_MOD,   ONLY: JPIB_K
  USE :: HOOKS_MOD,           ONLY: HOOKS_T
  USE :: METADATA_BASE_MOD,   ONLY: METADATA_BASE_A
  USE :: GRIB_METADATA_MOD,   ONLY: GRIB_METADATA_T
  USE :: LOG_METADATA_MOD,    ONLY: LOG_METADATA_T

  ! Symbols imported by the preprocessor for debugging purposes
  PP_DEBUG_USE_VARS

  ! Symbols imported by the preprocessor for logging purposes
  PP_LOG_USE_VARS

  ! Symbols imported by the preprocessor for tracing purposes
  PP_TRACE_USE_VARS

IMPLICIT NONE

  !> Dummy arguments
  CLASS(METADATA_BASE_A), POINTER, INTENT(OUT)   :: OUT_METADATA
  CHARACTER(LEN=*),                INTENT(IN)    :: KIND
  CHARACTER(LEN=*),                INTENT(IN)    :: SAMPLE_NAME
  TYPE(HOOKS_T),                   INTENT(INOUT) :: HOOKS

  !> Function result
  INTEGER(KIND=JPIB_K) :: RET

  !> Local variables
  INTEGER(KIND=JPIB_K) :: ALLOC_STATUS
  CHARACTER(LEN=:), ALLOCATABLE :: ERRMSG
  LOGICAL :: INITIALIZED

  !> Local error flag
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNKNOWN_TYPE=1_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_ALLOCATE=2_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_INITIALIZE=3_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED=5_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED=6_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED=7_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_CHECK_INITIALIZATION=8_JPIB_K

  ! Local variables declared by the preprocessor for debugging purposes
  PP_DEBUG_DECL_VARS

  ! Local variables declared by the preprocessor for logging purposes
  PP_LOG_DECL_VARS

  ! Local variables declared by the preprocessor for tracing purposes
  PP_TRACE_DECL_VARS

  ! Trace begin of procedure
  PP_TRACE_ENTER_PROCEDURE()

  ! Initialization of good path return value
  PP_SET_ERR_SUCCESS( RET )

  ! Error handling
  PP_DEBUG_CRITICAL_COND_THROW( ASSOCIATED(OUT_METADATA), ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED )

  ! Initialize the output metadata
  NULLIFY( OUT_METADATA )

  SELECT CASE(KIND)

  CASE ('grib')
    ! According to the type of metadata create the right object
    ALLOCATE( GRIB_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CASE ('log')
    ! According to the type of metadata create the right object
    ALLOCATE( LOG_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CASE DEFAULT
    PP_DEBUG_CRITICAL_THROW( ERRFLAG_UNKNOWN_TYPE )
  END SELECT

  ! Initialize frome sample name
  PP_TRYCALL(ERRFLAG_UNABLE_TO_INITIALIZE) OUT_METADATA%INIT_FROM_SAMPLE_NAME( SAMPLE_NAME, HOOKS )

  ! Erro handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.ASSOCIATED(OUT_METADATA), ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED )

  ! Check initialization
  PP_TRYCALL(ERRFLAG_UNABLE_CHECK_INITIALIZATION) OUT_METADATA%INITIALIZED( INITIALIZED, HOOKS )
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.INITIALIZED, ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED )

  ! Trace end of procedure (on success)
  PP_TRACE_EXIT_PROCEDURE_ON_SUCCESS()

  ! Exit point (On success)
  RETURN

! Error handler
PP_ERROR_HANDLER

  ! Initialization of bad path return value
  PP_SET_ERR_FAILURE( RET )

#if defined( PP_DEBUG_ENABLE_ERROR_HANDLING )
!$omp critical(ERROR_HANDLER)

  BLOCK

    ! Error handling variables
    PP_DEBUG_PUSH_FRAME()

    ! Handle different errors
    SELECT CASE(ERRIDX)
    CASE (ERRFLAG_UNKNOWN_TYPE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unknown metadata type' )
    CASE (ERRFLAG_UNABLE_TO_ALLOCATE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to allocate memory for metadata' )
      IF ( ALLOCATED(ERRMSG) ) THEN
        PP_DEBUG_PUSH_MSG_TO_FRAME( 'error message: ' // TRIM(ERRMSG) )
      ENDIF
    CASE (ERRFLAG_UNABLE_TO_INITIALIZE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to initialize metadata' )
    CASE (ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata already associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not initialized' )
    CASE (ERRFLAG_UNABLE_CHECK_INITIALIZATION)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to check initialization' )
    CASE DEFAULT
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unhandled error' )
    END SELECT

    ! Trace end of procedure (on error)
    PP_TRACE_EXIT_PROCEDURE_ON_ERROR()

    ! Write the error message and stop the program
    PP_DEBUG_ABORT

  END BLOCK

!$omp end critical(ERROR_HANDLER)
#endif

  ! Exit point (on error)
  RETURN


END FUNCTION MAKE_METADATA_FROM_SAMPLE_NAME
#undef PP_PROCEDURE_NAME
#undef PP_PROCEDURE_TYPE


#define PP_PROCEDURE_TYPE 'FUNCTION'
#define PP_PROCEDURE_NAME 'CLONE_METADATA'
PP_THREAD_SAFE FUNCTION  CLONE_METADATA( IN_METADATA, OUT_METADATA, HOOKS ) RESULT(RET)

  ! Symbols imported from other modules within the project.
  USE :: DATAKINDS_DEF_MOD,   ONLY: JPIB_K
  USE :: HOOKS_MOD,           ONLY: HOOKS_T
  USE :: METADATA_BASE_MOD,   ONLY: METADATA_BASE_A
  USE :: GRIB_METADATA_MOD,   ONLY: GRIB_METADATA_T
  USE :: MULTIO_METADATA_MOD, ONLY: MULTIO_METADATA_T
  USE :: LOG_METADATA_MOD,    ONLY: LOG_METADATA_T
  USE :: MULTIO_API,          ONLY: MULTIO_HANDLE

  ! Symbols imported by the preprocessor for debugging purposes
  PP_DEBUG_USE_VARS

  ! Symbols imported by the preprocessor for logging purposes
  PP_LOG_USE_VARS

  ! Symbols imported by the preprocessor for tracing purposes
  PP_TRACE_USE_VARS

IMPLICIT NONE

  !> Dummy arguments
  CLASS(METADATA_BASE_A), POINTER, INTENT(IN)    :: IN_METADATA
  CLASS(METADATA_BASE_A), POINTER, INTENT(OUT)   :: OUT_METADATA
  TYPE(HOOKS_T),                   INTENT(INOUT) :: HOOKS

  !> Function result
  INTEGER(KIND=JPIB_K) :: RET

  !> Local variables
  INTEGER(KIND=JPIB_K) :: ALLOC_STATUS
  CHARACTER(LEN=:), ALLOCATABLE :: ERRMSG
  LOGICAL :: INITIALIZED
  TYPE(MULTIO_HANDLE), POINTER :: MIOH

  !> Local error flag
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNKNOWN_TYPE=1_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_ALLOCATE=2_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_INITIALIZE=3_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_INPUT_METADATA_NOT_ASSOCIATED=4_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED=5_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED=6_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED=7_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_CHECK_INITIALIZATION=8_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_GET_MIOH=9_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_SET_MIOH=10_JPIB_K

  ! Local variables declared by the preprocessor for debugging purposes
  PP_DEBUG_DECL_VARS

  ! Local variables declared by the preprocessor for logging purposes
  PP_LOG_DECL_VARS

  ! Local variables declared by the preprocessor for tracing purposes
  PP_TRACE_DECL_VARS

  ! Trace begin of procedure
  PP_TRACE_ENTER_PROCEDURE()

  ! Initialization of good path return value
  PP_SET_ERR_SUCCESS( RET )

  ! Error handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT. ASSOCIATED(IN_METADATA), ERRFLAG_INPUT_METADATA_NOT_ASSOCIATED )
  PP_DEBUG_CRITICAL_COND_THROW( ASSOCIATED(OUT_METADATA),      ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED )

  ! Initialize the output metadata
  NULLIFY( OUT_METADATA )

  ! According to the type of metadata create the right object
  SELECT TYPE(A=>IN_METADATA)
  CLASS IS (GRIB_METADATA_T)
    ALLOCATE( GRIB_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CLASS IS (MULTIO_METADATA_T)
    ALLOCATE( MULTIO_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CLASS IS (LOG_METADATA_T)
    ALLOCATE( LOG_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CLASS DEFAULT
    PP_DEBUG_CRITICAL_THROW( ERRFLAG_UNKNOWN_TYPE )
  END SELECT

  ! Erro handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.ASSOCIATED(OUT_METADATA), ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED )

  ! Copy the metadata (Need this instead of allocating with source because the source
  ! need to be cloned/duplicated, not just copied - especially for grib handles)
  PP_TRYCALL(ERRFLAG_UNABLE_TO_INITIALIZE) OUT_METADATA%INIT_FROM_METADATA( IN_METADATA, HOOKS )

  ! Check initialization
  PP_TRYCALL(ERRFLAG_UNABLE_CHECK_INITIALIZATION) OUT_METADATA%INITIALIZED( INITIALIZED, HOOKS )
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.INITIALIZED, ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED )

  ! Trace end of procedure (on success)
  PP_TRACE_EXIT_PROCEDURE_ON_SUCCESS()

  ! Exit point (On success)
  RETURN

! Error handler
PP_ERROR_HANDLER

  ! Initialization of bad path return value
  PP_SET_ERR_FAILURE( RET )

#if defined( PP_DEBUG_ENABLE_ERROR_HANDLING )
!$omp critical(ERROR_HANDLER)

  BLOCK

    ! Error handling variables
    PP_DEBUG_PUSH_FRAME()

    ! Handle different errors
    SELECT CASE(ERRIDX)
    CASE (ERRFLAG_UNKNOWN_TYPE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unknown metadata type' )
    CASE (ERRFLAG_UNABLE_TO_ALLOCATE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to allocate memory for metadata' )
      IF ( ALLOCATED(ERRMSG) ) THEN
        PP_DEBUG_PUSH_MSG_TO_FRAME( 'error message: ' // TRIM(ERRMSG) )
      ENDIF
    CASE (ERRFLAG_UNABLE_TO_INITIALIZE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to initialize metadata' )
    CASE (ERRFLAG_INPUT_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'input metadata not associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata already associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not initialized' )
    CASE (ERRFLAG_UNABLE_CHECK_INITIALIZATION)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to check initialization' )
    CASE (ERRFLAG_UNABLE_GET_MIOH)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to get multio handle' )
    CASE (ERRFLAG_UNABLE_SET_MIOH)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to set multio handle' )
    CASE DEFAULT
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unhandled error' )
    END SELECT

    ! Trace end of procedure (on error)
    PP_TRACE_EXIT_PROCEDURE_ON_ERROR()

    ! Write the error message and stop the program
    PP_DEBUG_ABORT

  END BLOCK

!$omp end critical(ERROR_HANDLER)
#endif

  ! Exit point (on error)
  RETURN


END FUNCTION CLONE_METADATA
#undef PP_PROCEDURE_NAME
#undef PP_PROCEDURE_TYPE



#define PP_PROCEDURE_TYPE 'FUNCTION'
#define PP_PROCEDURE_NAME 'CLONE_METADATA_AND_LOAD_SAMPLE_NAME'
PP_THREAD_SAFE FUNCTION  CLONE_METADATA_AND_LOAD_SAMPLE_NAME( IN_METADATA, OUT_METADATA, SAMPLE_NAME, HOOKS ) RESULT(RET)

  ! Symbols imported from other modules within the project.
  USE :: DATAKINDS_DEF_MOD,   ONLY: JPIB_K
  USE :: HOOKS_MOD,           ONLY: HOOKS_T
  USE :: METADATA_BASE_MOD,   ONLY: METADATA_BASE_A
  USE :: GRIB_METADATA_MOD,   ONLY: GRIB_METADATA_T
  USE :: MULTIO_METADATA_MOD, ONLY: MULTIO_METADATA_T
  USE :: LOG_METADATA_MOD,    ONLY: LOG_METADATA_T
  USE :: MULTIO_API,          ONLY: MULTIO_HANDLE

  ! Symbols imported by the preprocessor for debugging purposes
  PP_DEBUG_USE_VARS

  ! Symbols imported by the preprocessor for logging purposes
  PP_LOG_USE_VARS

  ! Symbols imported by the preprocessor for tracing purposes
  PP_TRACE_USE_VARS

IMPLICIT NONE

  !> Dummy arguments
  CLASS(METADATA_BASE_A), POINTER, INTENT(IN)    :: IN_METADATA
  CLASS(METADATA_BASE_A), POINTER, INTENT(OUT)   :: OUT_METADATA
  CHARACTER(LEN=*),                INTENT(IN)    :: SAMPLE_NAME
  TYPE(HOOKS_T),                   INTENT(INOUT) :: HOOKS

  !> Function result
  INTEGER(KIND=JPIB_K) :: RET

  !> Local variables
  INTEGER(KIND=JPIB_K) :: ALLOC_STATUS
  CHARACTER(LEN=:), ALLOCATABLE :: ERRMSG
  LOGICAL :: INITIALIZED
  TYPE(MULTIO_HANDLE), POINTER :: MIOH

  !> Local error flag
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNKNOWN_TYPE=1_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_ALLOCATE=2_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_INITIALIZE=3_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_INPUT_METADATA_NOT_ASSOCIATED=4_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED=5_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED=6_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED=7_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_CHECK_INITIALIZATION=8_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_GET_MIOH=9_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_SET_MIOH=10_JPIB_K

  ! Local variables declared by the preprocessor for debugging purposes
  PP_DEBUG_DECL_VARS

  ! Local variables declared by the preprocessor for logging purposes
  PP_LOG_DECL_VARS

  ! Local variables declared by the preprocessor for tracing purposes
  PP_TRACE_DECL_VARS

  ! Trace begin of procedure
  PP_TRACE_ENTER_PROCEDURE()

  ! Initialization of good path return value
  PP_SET_ERR_SUCCESS( RET )

  ! Error handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT. ASSOCIATED(IN_METADATA), ERRFLAG_INPUT_METADATA_NOT_ASSOCIATED )
  PP_DEBUG_CRITICAL_COND_THROW( ASSOCIATED(OUT_METADATA),      ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED )

  ! Initialize the output metadata
  NULLIFY( OUT_METADATA )

  ! According to the type of metadata create the right object
  SELECT TYPE(A=>IN_METADATA)
  CLASS IS (GRIB_METADATA_T)
    ALLOCATE( GRIB_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CLASS IS (MULTIO_METADATA_T)
    ALLOCATE( MULTIO_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
    SELECT TYPE(B => OUT_METADATA)
    CLASS IS (MULTIO_METADATA_T)
      PP_TRYCALL(ERRFLAG_UNABLE_GET_MIOH) A%GET_MULTIO_HANDLE( MIOH, HOOKS )
      PP_TRYCALL(ERRFLAG_UNABLE_SET_MIOH) B%SET_MULTIO_HANDLE( MIOH, HOOKS )
    CLASS DEFAULT
      PP_DEBUG_CRITICAL_THROW( ERRFLAG_UNKNOWN_TYPE )
    ENDSELECT
  CLASS IS (LOG_METADATA_T)
    ALLOCATE( LOG_METADATA_T::OUT_METADATA, STAT=ALLOC_STATUS, ERRMSG=ERRMSG )
    PP_DEBUG_CRITICAL_COND_THROW( ALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_ALLOCATE )
  CLASS DEFAULT
    PP_DEBUG_CRITICAL_THROW( ERRFLAG_UNKNOWN_TYPE )
  END SELECT

  ! Erro handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.ASSOCIATED(OUT_METADATA), ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED )

  ! Copy the metadata (Need this instead of allocating with source because the source
  ! need to be cloned/duplicated, not just copied - especially for grib handles)
  PP_TRYCALL(ERRFLAG_UNABLE_TO_INITIALIZE) OUT_METADATA%INIT_FROM_SAMPLE_NAME( SAMPLE_NAME, HOOKS )

  ! Check initialization
  PP_TRYCALL(ERRFLAG_UNABLE_CHECK_INITIALIZATION) OUT_METADATA%INITIALIZED( INITIALIZED, HOOKS )
  PP_DEBUG_CRITICAL_COND_THROW( .NOT.INITIALIZED, ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED )

  ! Trace end of procedure (on success)
  PP_TRACE_EXIT_PROCEDURE_ON_SUCCESS()

  ! Exit point (On success)
  RETURN

! Error handler
PP_ERROR_HANDLER

  ! Initialization of bad path return value
  PP_SET_ERR_FAILURE( RET )

#if defined( PP_DEBUG_ENABLE_ERROR_HANDLING )
!$omp critical(ERROR_HANDLER)

  BLOCK

    ! Error handling variables
    PP_DEBUG_PUSH_FRAME()

    ! Handle different errors
    SELECT CASE(ERRIDX)
    CASE (ERRFLAG_UNKNOWN_TYPE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unknown metadata type' )
    CASE (ERRFLAG_UNABLE_TO_ALLOCATE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to allocate memory for metadata' )
      IF ( ALLOCATED(ERRMSG) ) THEN
        PP_DEBUG_PUSH_MSG_TO_FRAME( 'error message: ' // TRIM(ERRMSG) )
      ENDIF
    CASE (ERRFLAG_UNABLE_TO_INITIALIZE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to initialize metadata' )
    CASE (ERRFLAG_INPUT_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'input metadata not associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_ALREADY_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata already associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not associated' )
    CASE (ERRFLAG_OUTPUT_METADATA_NOT_INITIALIZED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'output metadata not initialized' )
    CASE (ERRFLAG_UNABLE_CHECK_INITIALIZATION)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to check initialization' )
    CASE (ERRFLAG_UNABLE_GET_MIOH)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to get multio handle' )
    CASE (ERRFLAG_UNABLE_SET_MIOH)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to set multio handle' )
    CASE DEFAULT
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unhandled error' )
    END SELECT

    ! Trace end of procedure (on error)
    PP_TRACE_EXIT_PROCEDURE_ON_ERROR()

    ! Write the error message and stop the program
    PP_DEBUG_ABORT

  END BLOCK

!$omp end critical(ERROR_HANDLER)
#endif

  ! Exit point (on error)
  RETURN


END FUNCTION CLONE_METADATA_AND_LOAD_SAMPLE_NAME
#undef PP_PROCEDURE_NAME
#undef PP_PROCEDURE_TYPE


#define PP_PROCEDURE_TYPE 'FUNCTION'
#define PP_PROCEDURE_NAME 'DESTROY_METADATA'
PP_THREAD_SAFE FUNCTION  DESTROY_METADATA( METADATA, HOOKS ) RESULT(RET)

  ! Symbols imported from other modules within the project.
  USE :: DATAKINDS_DEF_MOD,   ONLY: JPIB_K
  USE :: HOOKS_MOD,           ONLY: HOOKS_T
  USE :: METADATA_BASE_MOD,   ONLY: METADATA_BASE_A

  ! Symbols imported by the preprocessor for debugging purposes
  PP_DEBUG_USE_VARS

  ! Symbols imported by the preprocessor for logging purposes
  PP_LOG_USE_VARS

  ! Symbols imported by the preprocessor for tracing purposes
  PP_TRACE_USE_VARS

IMPLICIT NONE

  !> Dummy arguments
  CLASS(METADATA_BASE_A), POINTER, INTENT(INOUT) :: METADATA
  TYPE(HOOKS_T),                   INTENT(INOUT) :: HOOKS

  !> Function result
  INTEGER(KIND=JPIB_K) :: RET

  !> Local variables
  INTEGER(KIND=JPIB_K) :: DEALLOC_STATUS
  CHARACTER(LEN=:), ALLOCATABLE :: ERRMSG
  LOGICAL :: INITIALIZED

  !> Local error flag
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_METADATA_NOT_ASSOCIATED=1_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_FREE_METADATA=2_JPIB_K
  INTEGER(KIND=JPIB_K), PARAMETER :: ERRFLAG_UNABLE_TO_DEALLOCATE=3_JPIB_K

  ! Local variables declared by the preprocessor for debugging purposes
  PP_DEBUG_DECL_VARS

  ! Local variables declared by the preprocessor for logging purposes
  PP_LOG_DECL_VARS

  ! Local variables declared by the preprocessor for tracing purposes
  PP_TRACE_DECL_VARS

  ! Trace begin of procedure
  PP_TRACE_ENTER_PROCEDURE()

  ! Initialization of good path return value
  PP_SET_ERR_SUCCESS( RET )

  ! Error handling
  PP_DEBUG_CRITICAL_COND_THROW( .NOT. ASSOCIATED(METADATA), ERRFLAG_METADATA_NOT_ASSOCIATED )

  ! Free the metadata
  PP_TRYCALL(ERRFLAG_UNABLE_TO_FREE_METADATA) METADATA%DESTROY( HOOKS )

  ! Deallocate the metadata
  DEALLOCATE( METADATA, STAT=DEALLOC_STATUS, ERRMSG=ERRMSG )
  PP_DEBUG_CRITICAL_COND_THROW( DEALLOC_STATUS.NE.0, ERRFLAG_UNABLE_TO_DEALLOCATE )

  ! Trace end of procedure (on success)
  PP_TRACE_EXIT_PROCEDURE_ON_SUCCESS()

  ! Exit point (On success)
  RETURN

! Error handler
PP_ERROR_HANDLER

  ! Initialization of bad path return value
  PP_SET_ERR_FAILURE( RET )

#if defined( PP_DEBUG_ENABLE_ERROR_HANDLING )
!$omp critical(ERROR_HANDLER)

  BLOCK

    ! Error handling variables
    PP_DEBUG_PUSH_FRAME()

    ! Handle different errors
    SELECT CASE(ERRIDX)
    CASE (ERRFLAG_METADATA_NOT_ASSOCIATED)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'metadata not associated' )
    CASE (ERRFLAG_UNABLE_TO_FREE_METADATA)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to free metadata' )
    CASE (ERRFLAG_UNABLE_TO_DEALLOCATE)
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unable to deallocate metadata' )
      IF ( ALLOCATED(ERRMSG) ) THEN
        PP_DEBUG_PUSH_MSG_TO_FRAME( 'error message: ' // TRIM(ERRMSG) )
        DEALLOCATE(ERRMSG, STAT=DEALLOC_STATUS)
      ENDIF
    CASE DEFAULT
      PP_DEBUG_PUSH_MSG_TO_FRAME( 'unhandled error' )
    END SELECT

    ! Trace end of procedure (on error)
    PP_TRACE_EXIT_PROCEDURE_ON_ERROR()

    ! Write the error message and stop the program
    PP_DEBUG_ABORT

  END BLOCK

!$omp end critical(ERROR_HANDLER)
#endif

  ! Exit point (on error)
  RETURN


END FUNCTION DESTROY_METADATA
#undef PP_PROCEDURE_NAME
#undef PP_PROCEDURE_TYPE

END MODULE METADATA_FACTORY_MOD
#undef PP_SECTION_NAME
#undef PP_SECTION_TYPE
#undef PP_FILE_NAME
